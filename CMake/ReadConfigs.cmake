
# Copyright (c) 2012 Stefan Eilemann <Stefan.Eilemann@epfl.ch>
# Does a use_external(..) for each config*/*.cmake project.

include(UseExternal)
include(CreateDependencyGraph)

set(_configs)
file(GLOB _dirs "${CMAKE_SOURCE_DIR}/config*")
list(SORT _dirs)
foreach(_dir ${_dirs})
  if(IS_DIRECTORY "${_dir}" AND NOT "${_dir}" MATCHES "config.local$")
    file(GLOB _files "${_dir}/*.cmake")
    foreach(_config ${_files})
      include(${_config})
    endforeach()
  endif()
endforeach()

if(IS_DIRECTORY ${CMAKE_SOURCE_DIR}/config.local)
  message(STATUS "Applying local override configuration from config.local")
  file(GLOB _files "config.local/*.cmake")
  foreach(_config ${_files})
    include(${_config})
  endforeach()
endif()

foreach(_file ${_configs})
  get_filename_component(_dir ${_file} PATH)
endforeach()

set(_configdone)
foreach(_dir ${_dirs})
  if(IS_DIRECTORY "${_dir}" AND NOT "${_dir}" MATCHES "config.local$")
    message(STATUS "Loading ${_dir}")
    if(_dir STREQUAL "${CMAKE_SOURCE_DIR}/config")
      set(_dest "${CMAKE_SOURCE_DIR}/src/eyescale/images")
    else()
      set(_dest "${_dir}")
    endif()

    create_dependency_graph_start(${_dir})
    file(GLOB _files "${_dir}/*.cmake")
    foreach(_configfile ${_files})
      string(REPLACE ".cmake" "" _config ${_configfile})
      get_filename_component(_config ${_config} NAME)

      set(_configfound)
      list(FIND _configdone ${_config} _configfound)
      if(_configfound EQUAL -1)
        list(APPEND _configdone ${_config})
        create_dependency_graph(${_dir} ${_dest} ${_config})

        string(TOUPPER ${_config} _CONFIG)
        set(${_CONFIG}_CONFIGFILE "${_configfile}")
        use_external(${_config})
      endif()
    endforeach()
    create_dependency_graph_end(${_dir} ${_dest})

    # Output configured projects:
    if(BUILDING)
      list(SORT BUILDING)
      set(TEXT "Building")
      foreach(PROJECT ${BUILDING})
        set(TEXT "${TEXT} ${PROJECT}")
      endforeach()
      message(STATUS ${TEXT})
      set(BUILDING)
    endif()
  endif()
endforeach()
